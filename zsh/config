#!/bin/zsh


### Options ################################################
# I got this stuff from random browsing through man zshoptions

setopt AUTO_CD       # '$ foo' means '$ cd foo' if 'foo' is not a command
setopt AUTO_PUSHD    # 'cd' is now cooler
setopt PUSHD_IGNORE_DUPS
DIRSTACKSIZE=15

### Prompt #################################################
autoload -U colors
colors
setopt PROMPT_SUBST
setopt TRANSIENT_RPROMPT

prompt_zsh_level () {
  if [[ -n $TMUX && $SHLVL > 2 ]]; then
    printf '∎'%.0s {3..$SHLVL}
  elif [[ -z $TMUX && $SHLVL > 1 ]]; then
    printf '∎'%.0s {2..$SHLVL}
  fi
}

prompt_current_context () {
    if [[ -n $c_current ]]; then
        print " ($c_current)"
    fi
}

PROMPT='%(#.%{$fg_bold[yellow]%}.%{$fg_bold[white]%})%(?..%{$fg_bold[red]%})$(prompt_zsh_level)%(#.∷.→)%{$reset_color%} '
RPROMPT='%{$fg_bold[green]%}%c%{$fg_bold[white]%}:%{$fg_bold[blue]%}%m %{$fg_bold[red]%}$(vcprompt -f "[%b%m%u]")%{$reset_color%}%{$fg_bold[yellow]%}$(prompt_current_context)%{$reset_color%}'
PROMPT_EOL_MARK="%{${fg_bold[yellow]}%}¶%{$reset_color%}"
PS2=$'%{\e[38;5;240m%}…%{$reset_color%} '

# Override prompt for Beamer or Emacs
case "$ITERM_PROFILE" in
    Beamer)
        PROMPT='➔ '
        RPROMPT=''
        ;;
    tkn)
        RPROMPT=''
        ;;
esac

if [ -n "$EMACS" ] ; then
  PROMPT='$ '
fi

### Options from PeepCode ##################################
setopt HIST_IGNORE_DUPS     # ignore duplication command history list
setopt SHARE_HISTORY        # share command history data
setopt HIST_IGNORE_SPACE    # Don't push commands starting with space in history
setopt APPEND_HISTORY       # write history only when closing
setopt EXTENDED_HISTORY     # add more info

setopt BRACECCL             # expand {a-z.} to a b ... x y z .

setopt NOCLOBBER            # don't allow rediriection to clobber files
#setopt NOMULTIOS            # MULTIOS

setopt CORRECT

[[ -z "$TMUX" ]] && setopt IGNORE_EOF # Don't exit on C-d outside tmux

autoload -U url-quote-magic
zle -N self-insert url-quote-magic

HISTFILE=~/.zsh_history
SAVEHIST=30000
HISTSIZE=30000

REPORTTIME=10 # Show elapsed time if command took more than X seconds
LISTMAX=0 # ask to complete if top of list would scroll off screen

# Make CTRL-W delete after other chars, not just spaces
WORDCHARS=${WORDCHARS//[&=\/;\!#%\{]}

bindkey -e

autoload -U edit-command-line
zle -N edit-command-line

bindkey '\ee' edit-command-line
bindkey '\eq' push-line-or-edit

# From the Zsh User Guide
setopt CD_ABLE_VARS

which bat > /dev/null && export MANPAGER="sh -c 'col -bx | bat -l man -p'"

function tmux-page-up() {
  [[ -n $TMUX ]] && tmux copy-mode -u
}

zle -N tmux-page-up

bindkey "\e[5~" tmux-page-up

typeset -U path
