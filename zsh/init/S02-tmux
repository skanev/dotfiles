#!/usr/bin/env zsh

[[ -z $TMUX ]] && return

_tmux_pane_words() {
  local expl
  local -a w

  w=( ${(u)=$(tmux capture-pane \; save-buffer - \; delete-buffer)} )
  _wanted values expl 'words from current tmux pane' compadd -a w
}

_tmux_pane_paths() {
  local expl
  local -a paths

  paths=( $(~/.scripts/tmux/pane-paths ) )
  _wanted values expl 'paths from current tmux pane' compadd -a paths
}

zle -C tmux-pane-words-prefix   complete-word _generic
zle -C tmux-pane-words-anywhere complete-word _generic
zle -C tmux-pane-paths          complete-word _generic

bindkey '^X^X' tmux-pane-words-prefix
bindkey '^X^A' tmux-pane-words-anywhere
bindkey '^X^P' tmux-pane-paths

zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' completer _tmux_pane_words
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' ignore-line current
zstyle ':completion:tmux-pane-words-anywhere:*' matcher-list 'b:=* m:{A-Za-z}={a-zA-Z}'

zstyle ':completion:tmux-pane-paths:*' completer _tmux_pane_paths

rerun() {
  local cmd=$(~/.scripts/stalker poke rerun)
  if [[ -z "$cmd" ]]; then
    echo "All good!"
  else
    echo "> $cmd"
    eval $cmd
  fi
}

stalk() {
  tmux pipe-pane '~/.scripts/stalker stalk'
}
